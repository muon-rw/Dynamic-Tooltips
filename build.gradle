plugins {
	id 'fabric-loom' version '1.10-SNAPSHOT'
	id 'maven-publish'
	id "me.modmuss50.mod-publish-plugin" version "0.8.4"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven { url = 'https://maven.parchmentmc.org' }
	maven { url 'https://jitpack.io' }
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
	maven { url "https://cursemaven.com" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-1.21.1:2024.11.17@zip")
	}
	// mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation("com.github.bawnorton.mixinsquared:mixinsquared-fabric:0.2.0")
	include("com.github.bawnorton.mixinsquared:mixinsquared-fabric:0.2.0")
	annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-fabric:0.2.0")

	// Compatibility Testing
	modLocalRuntime "curse.maven:tieredz-615948:6130503"
	modLocalRuntime "curse.maven:libz-841617:5712924"
	modLocalRuntime "curse.maven:cloth-config-348521:5729125"

	modImplementation "curse.maven:better-combat-by-daedelus-639842:6097300"
	modLocalRuntime "curse.maven:playeranimator-658587:6024464"

	modImplementation "curse.maven:ranged-weapon-api-962162:6077072"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}
}

publishMods {
	file = remapJar.archiveFile
	type = STABLE
	modLoaders.add("fabric")
	changelog = new File("$project.rootDir/changelog.md").getText('UTF-8')

	curseforge {
		projectId = "123456"
		projectSlug = "dynamic-tooltips"
		accessToken = providers.environmentVariable("CF_TOKEN")
		minecraftVersions.add("1.21.1")
		requires("fabric-api")
		clientRequired = true
		serverRequired = false
	}
	modrinth {
		projectId = "dynamic-tooltips"
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
		minecraftVersions.add("1.21.1")
	}
}